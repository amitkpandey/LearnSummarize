apply plugin: 'com.android.application'
apply plugin: 'com.antfortune.freeline'
apply plugin: 'realm-android'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.chenshun.learnsummarize"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.1"
        buildConfigField "String", "GIT_VERSION_DETAIL", "\"${getGitVersion()}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true" // show Log
            buildConfigField "boolean", "IS_DEBUG", "true"  // is in debug model

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false" // not show Log
            buildConfigField "boolean", "IS_DEBUG", "false"  // is in debug model

            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true // 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding { // 设置支持数据绑定框架
        enabled = true
    }
    freeline {
        hack true
    }
    lintOptions {
        abortOnError false
        ignoreWarnings true
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "8g"
    }
    useLibrary 'org.apache.http.legacy'
    productFlavors { // 多渠道打包
        xiaomi {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
        }
        _360 {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "_360"]
        }
        baidu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "baidu"]
        }
        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
    }
}

greendao {
    schemaVersion 1 // 标记数据库版本号，每次修改 entity/database 该值必须增加，默认为1
    daoPackage "com.chenshun.learnsummarize.ui.activity.database.greendao" // DAOs, DaoMaster, and DaoSession 存放的包路径，默认是源码目录
    targetGenDir "src/main/java" // 生成数据库文件的目录
}

ext {
    supportLibVersion = "25.2.0"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile 'com.android.support:multidex:1.0.0'
    testCompile 'junit:junit:4.12'
    // network:okhttp-OkGo
    compile 'com.lzy.net:okgo:2.1.4'
    compile 'com.lzy.net:okrx:0.1.2'
    compile 'com.lzy.net:okserver:1.1.3'
    // load images:universalimageloader
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.2'
    // circle image
    compile 'de.hdodenhof:circleimageview:1.3.0'
    // json
    compile 'com.alibaba:fastjson:1.2.17'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    // leading page:
    compile 'com.github.paolorotolo:appintro:4.1.0'
    // analytics
    compile 'com.umeng.analytics:analytics:latest.integration'
    // squareup:check leak memory
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    // freeline
    debugCompile 'com.antfortune.freeline:runtime:0.8.2'
    releaseCompile 'com.antfortune.freeline:runtime-no-op:0.8.2'
    testCompile 'com.antfortune.freeline:runtime-no-op:0.8.2'
    // database
    compile 'io.realm:android-adapters:1.4.0'
    compile 'org.greenrobot:greendao:3.0.1'
    compile 'org.greenrobot:greendao-generator:3.0.0'
    // when app crash
    compile 'cat.ereza:customactivityoncrash:1.5.0'
    // recyclerview adapter
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.8.4'
    // spinner
    compile 'com.jaredrummler:material-spinner:1.1.0'
    // button
    compile 'com.kyleduo.switchbutton:library:1.4.5'
    // textview
    compile 'com.tomer:fadingtextview:1.6'
}

/**
 * Git 采用的是 SHA散列码 作为版本号，因此它没有顺序的版本号
 * @return
 */
def getGitVersion() {
    return 'git rev-parse HEAD'.execute().text.trim();
}

/**
 * get svn version info
 * @return
 */
def getSVNVersion() {
    return 'svn info'.execute().text.trim().replace("\r", "").split("\n")[6];
}
